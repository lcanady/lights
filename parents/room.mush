/*
#############################################################################
### CITY OF LIGHTS ROOM PARENT ##############################################

  This file represents the room parent for the City of Lights.  After
  this file is quoted, make sure to go into your netrhost.conf file and
  set your room_parent or global_room_parent so that all newly dug rooms
  automatically pick up the formatting.

  This file was written to be processed with mush-format
    * https://github.com/digibear-io/mush-format

#############################################################################
*/

// Make sure the rp tag isn't taken before we dig the room parent
// for development, upgrades, etc, etc.

@if not(isdbref(tag(rp))) = {
    @dig Room Parent <RP>;
    @tag/add rp = lastcreate(me, R);
    @flags rp = safe float;
  }

@nameformat #rp =
  [u(
    #gfo/header,
    cname(%!),,
    if(hasflag(%!,IC), IC, OOC)
  )]

// Set the parent description defualt so we can manipulate
// the desc before showing it to the room contents.

@set #rp/desc = default
&formatdesc #rp =%r%0%r

@conformat #rp =
  [u(#gfo/header, Characters)]%r
  [space(3)]
  %ch[ljust(Name, 25)]
  [rjust(Idle, 5)]%b%b
  Title%cn%r
  [repeat(%ch%cb-%cn,77)]
  [iter(
      %# [lcon(%!/connected)],
      %r[u(%!/fn.player, ##)]
  )]%r
  [u(#gfo/header, Objects,,Exits)]
  [iter(

    // Basically I want to get a list of numbers here,
    // based on the largest number of dbrefs for things
    // and exits.

    lnum(
      max(
        words(setr(0, lcon(%!/object))),
        words(setr(1, lexits(%!)))
      )
    ),

    // Now we're going to use the current number as our index
    // to grab values from our lists.
    %r[ljc(u(%!/fn.things, extract(%q0, add(##, 1), 1)), 50)]
      [rjc(u(%!/fn.exits, extract(%q1, add(##, 1), 1)), 26)]
  )]

@exitformat #rp = [u(#gfo/header)]


// Format room player list.
&fn.player #rp =
  printf(
    $^3s$25-s$5s%b%b$38-s,
    u(#gfo/fn.marker,%0),
    cname(%0),
    objeval(%!, singletime(idle(%0))),
    objeval(%!, default(%0/caption, %ch%cxThe citizen.%cn))
  )

&fn.things #rp = 
  if(
    strlen(%0),
    [space(3)]%ch%cxa%cn [cname(%0)]
    [objeval(%!,udefault(%0/placement, %b%ch%cx%,sits here.%cn))]
  )

&fn.exits #rp = [edit(edit(cname(%0),<,%cy<%cn),>,%cy>%cn)]%b%b
